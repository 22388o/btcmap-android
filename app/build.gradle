apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 33
        versionName "1.8.3"
        resConfigs "en", "ru", "th"

        buildConfigField "String", "API_URL", "\"http://91.121.144.57:3000/api/v2/\""
        buildConfigField "boolean", "USE_DB_SNAPSHOT", "true"
        buildConfigField "String", "DATABASE_NAME", "\"coins.db\""
        buildConfigField "int", "DATABASE_VERSION", "22"
        buildConfigField "String", "GOOGLE_CLIENT_ID", "\"557789430086-hnt5bk65a636mgi7phhn5t9b1porppik.apps.googleusercontent.com\""

        resValue "string", "maps_api_key", "AIzaSyCFlG9gZj-6ja-CQC6cAgvQXq6fs0foYVQ"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile file(KEYSTORE_PATH)
            storePassword KEYSTORE_PASSWORD
            keyAlias "coins"
            keyPassword KEYSTORE_PASSWORD
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-dev"
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "USE_DB_SNAPSHOT", "false"
        }
        release {
            minifyEnabled true
            useProguard true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def projectName = 'coins'
            output.outputFile = file("$project.buildDir/apk/$projectName-$variant.versionName" + '.apk')
        }
    }

    // TODO remove after fix on Google side
    applicationVariants.all { variant ->
        def productFlavor = variant.productFlavors[0] != null ? "${variant.productFlavors[0].name.capitalize()}" : ""
        def buildType = "${variant.buildType.name.capitalize()}"
        tasks["compile${productFlavor}${buildType}UnitTestSources"].dependsOn(tasks["merge${productFlavor}${buildType}Assets"])
    }
}

dependencies {
    // Android support libraries
    compile "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:design:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:customtabs:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:cardview-v7:${rootProject.ext.supportLibVersion}"

    // Google Play services split client libs
    compile "com.google.android.gms:play-services-gcm:${rootProject.ext.googlePlayServicesVersion}"
    compile "com.google.android.gms:play-services-location:${rootProject.ext.googlePlayServicesVersion}"
    compile "com.google.android.gms:play-services-maps:${rootProject.ext.googlePlayServicesVersion}"
    compile "com.google.android.gms:play-services-auth:${rootProject.ext.googlePlayServicesVersion}"
    compile "com.google.firebase:firebase-crash:${rootProject.ext.googlePlayServicesVersion}"

    compile 'com.google.maps.android:android-maps-utils:0.4'

    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.5.0'

    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    compile 'com.jakewharton.timber:timber:4.1.1'

    compile 'com.google.code.gson:gson:2.8.0'

    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.squareup.picasso:picasso:2.5.2'

    provided 'com.jakewharton.auto.value:auto-value-annotations:1.4'
    annotationProcessor 'com.google.auto.value:auto-value:1.4'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'

    compile 'com.google.dagger:dagger:2.8'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.8'

    testCompile 'junit:junit:4.12'

    testCompile 'org.robolectric:robolectric:3.3.2'

    androidTestCompile('com.android.support.test:runner:0.5', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    androidTestCompile('com.android.support.test:rules:0.5', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    androidTestCompile ('com.android.support.test.espresso:espresso-contrib:2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }

    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
}

apply plugin: 'com.google.gms.google-services'