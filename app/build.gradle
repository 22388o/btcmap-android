apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'io.fabric'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.bubelov.coins"

        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        versionCode rootProject.versionCode
        versionName rootProject.versionName

        compileOptions {
            targetCompatibility 1.8
            sourceCompatibility 1.8
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/src/main/assets/schemas".toString()]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "API_URL", API_URL
        buildConfigField "Boolean", "MOCK_API", "false"

        def googleClientId = project.hasProperty('GOOGLE_CLIENT_ID') ? GOOGLE_CLIENT_ID : System.getenv('GOOGLE_CLIENT_ID')
        buildConfigField "String", "GOOGLE_CLIENT_ID", googleClientId != null ? googleClientId : ""

        buildConfigField "Float", "MAP_MARKER_ANCHOR_U", "$map_marker_anchor_u"
        buildConfigField "Float", "MAP_MARKER_ANCHOR_V", "$map_marker_anchor_v"

        buildConfigField "Double", "DEFAULT_LOCATION_LAT", "$default_location_lat"
        buildConfigField "Double", "DEFAULT_LOCATION_LON", "$default_location_lon"

        project.ext.set("archivesBaseName", "Coins-" + defaultConfig.versionName)

        manifestPlaceholders = [crashlyticsEnabled: true]
    }

    buildTypes {
        debug {
            manifestPlaceholders = [crashlyticsEnabled: false]
        }
        release {
            manifestPlaceholders = [crashlyticsEnabled: true]
        }
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"
    implementation "androidx.core:core-ktx:$ktxVersion"

    // Dependency injection
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"

    // Network
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:$retrofitCoroutinesVersion"
    implementation "com.squareup.retrofit2:retrofit-mock:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$loggingInterceptorVersion"

    // Cache
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // Navigation
    implementation "android.arch.navigation:navigation-ui-ktx:$navigationVersion"
    implementation "android.arch.navigation:navigation-fragment-ktx:$navigationVersion"

    // Scheduling
    implementation "android.arch.work:work-runtime-ktx:$workVersion"
    androidTestImplementation "android.arch.work:work-testing:$workVersion"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"

    // Compatibility
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.browser:browser:$browserVersion"

    // Google services
    implementation "com.google.android.gms:play-services-auth:$playServicesVersion"
    implementation "com.google.android.gms:play-services-maps:$playServicesVersion"
    implementation "com.google.firebase:firebase-core:$firebaseVersion"
    implementation "com.google.maps.android:android-maps-utils:$mapsUtilsVersion"

    // JSON
    implementation "com.google.code.gson:gson:$gsonVersion"

    // Images
    implementation "com.squareup.picasso:picasso:$picassoVersion"

    // Logging
    implementation "com.jakewharton.timber:timber:$timberVersion"

    // Crash reporting
    implementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion@aar") {
        transitive = true
    }

    // Unit tests
    testImplementation "junit:junit:$junitVersion"
    testImplementation "androidx.arch.core:core-testing:$coreTestingVersion"

    // Mocks
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockitoKotlinVersion"

    // Instrumented tests
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation "androidx.test:runner:$runnerVersion"
    androidTestImplementation "androidx.test:rules:$runnerVersion"
    androidTestImplementation "androidx.room:room-testing:$roomVersion"
}

apply plugin: 'com.google.gms.google-services'
