apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 34
        versionName "2.0"
        resConfigs "en", "ru", "th"

        buildConfigField "String", "API_URL", "\"http://91.121.144.57:3000/api/v3/\""
        buildConfigField "String", "GOOGLE_CLIENT_ID", GOOGLE_CLIENT_ID

        project.ext.set("archivesBaseName", "Coins-" + defaultConfig.versionName)

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        release {
            storeFile file(KEYSTORE_PATH)
            storePassword KEYSTORE_PASSWORD
            keyAlias KEYSTORE_KEY_ALIAS
            keyPassword KEYSTORE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-dev"
        }
        release {
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    // Kotlin support
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.19.1"
    implementation "org.jetbrains.anko:anko-commons:$anko_version"

    // Android architecture components
    implementation "android.arch.lifecycle:extensions:1.0.0-rc1"
    kapt "android.arch.lifecycle:compiler:1.0.0-rc1"
    testImplementation "android.arch.core:core-testing:1.0.0-rc1"
    implementation "android.arch.persistence.room:runtime:1.0.0-rc1"
    kapt "android.arch.persistence.room:compiler:1.0.0-rc1"

    // Android support libraries
    implementation "com.android.support:support-v13:$support_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:customtabs:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"

    // Google Play services split client libs
    implementation "com.google.android.gms:play-services-gcm:$play_services_version"
    implementation "com.google.android.gms:play-services-location:$play_services_version"
    implementation "com.google.android.gms:play-services-maps:$play_services_version"
    implementation "com.google.android.gms:play-services-auth:$play_services_version"
    implementation "com.google.firebase:firebase-core:$play_services_version"
    implementation "com.google.firebase:firebase-crash:$play_services_version"

    // Dependency injection
    implementation 'com.google.dagger:dagger:2.11'
    kapt 'com.google.dagger:dagger-compiler:2.11'
    kaptTest 'com.google.dagger:dagger-compiler:2.11'
    kapt 'com.google.dagger:dagger-android-processor:2.11'
    implementation 'com.google.dagger:dagger-android-support:2.11'

    // Api mapping
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'

    // Map utils
    implementation 'com.google.maps.android:android-maps-utils:0.5'

    // Image utils
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'de.hdodenhof:circleimageview:2.1.0'

    // JSON utils
    implementation 'com.google.code.gson:gson:2.8.0'

    // Logging
    implementation 'com.jakewharton.timber:timber:4.5.1'

    testImplementation 'junit:junit:4.12'

    testImplementation 'org.robolectric:robolectric:3.3.2'

    androidTestImplementation('com.android.support.test:runner:1.0.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    androidTestImplementation('com.android.support.test:rules:1.0.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:3.0.0') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }

    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
}

apply plugin: 'com.google.gms.google-services'